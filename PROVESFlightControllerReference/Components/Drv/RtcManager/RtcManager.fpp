# Type definition
module Drv {
    struct TimeData {
        Year: U32 @< Year value.
        Month: U32 @< Month value.
        Day: U32 @< Day value.
        Hour: U32 @< Hour value.
        Minute: U32 @< Minute value.
        Second: U32 @< Second value.
    }
}

@ Timebase parameter for the RtcManager component
# Under RTC so that it does not shadow the TimeBase generated in the DRV module
module Rtc {
    @ Parameter for timebase
    enum TimeBase: FwTimeBaseStoreType {
        TB_PROC_TIME = 1 @< timebase is in proc time
        TB_SC_TIME = 3   @< timebase is in RTC time
    } default TB_SC_TIME
}

# Port definition
module Drv {
    port TimeSet(t: TimeData)
    port TimeGet -> U32
    port AlarmSet(t: Fw.TimeValue) -> U32
    port AlarmCancel(ID: U16) -> U32
    port AlarmTriggered()
}

module Drv {
    @ Manages the real time clock
    passive component RtcManager {
        import Svc.Time

        ### PARAMETERS ###

        @ Time base as a parameter
        param TIMEBASE: Rtc.TimeBase default Rtc.TimeBase.TB_SC_TIME

        ### COMMANDS ###

        @ TIME_SET command to set the time on the RTC
        sync command TIME_SET(
            t: Drv.TimeData @< Set the time
        )

        @ ALARM_SET command to set an alarm on the RTC
        sync command ALARM_SET(
            t: Drv.TimeData @< Time to set the alarm for
        )

        @ ALARM_CANCEL command to cancel any set alarms on the RTC
        sync command ALARM_CANCEL(
            ID: U16 @< ID of the alarm to cancel
        )

        @ ALARM_LIST command to list all set alarms on the RTC
        sync command ALARM_LIST()

        ### EVENTS ###

        @ DeviceNotReady event indicates that the RTC is not ready
        event DeviceNotReady() severity warning high id 0 format "RTC not ready" throttle 1

        @ TimeSet event indicates that the time was set successfully
        event TimeSet(
            seconds: U32 @< Seconds since epoch
            useconds: U32 @< Microseconds
        ) severity activity high id 3 format "Time set on RTC, previous time: {}.{}"

        @ TimeBaseChanged event fires when the timebase parameter changes and indicates the new timebase
        event TimeBaseChanged(
            timeBase: Rtc.TimeBase @< The timebase now in use
        ) severity activity high id 18 format "Timebase changed to: {}"

        @ TimeNotSet event indicates that the time was not set successfully
        event TimeNotSet(rc: I32) severity warning high id 4 format "Time not set on RTC: {}"

        @ YearValidationFailed event indicates that the provided year is invalid
        event YearValidationFailed(
            year: U32 @< The invalid year
        ) severity warning high id 5 format "Provided year is invalid should be >= 1900: {}"

        @ MonthValidationFailed event indicates that the provided month is invalid
        event MonthValidationFailed(
            month: U32 @< The invalid month
        ) severity warning high id 6 format "Provided month is invalid should be in [1, 12]: {}"

        @ DayValidationFailed event indicates that the provided day is invalid
        event DayValidationFailed(
            day: U32 @< The invalid day
        ) severity warning high id 7 format "Provided day is invalid should be in [1, 31]: {}"

        @ HourValidationFailed event indicates that the provided hour is invalid
        event HourValidationFailed(
            hour: U32 @< The invalid hour
        ) severity warning high id 8 format "Provided hour is invalid should be in [0, 23]: {}"

        @ MinuteValidationFailed event indicates that the provided minute is invalid
        event MinuteValidationFailed(
            minute: U32 @< The invalid minute
        ) severity warning high id 9 format "Provided minute is invalid should be in [0, 59]: {}"

        @ SecondValidationFailed event indicates that the provided second is invalid
        event SecondValidationFailed(
            second: U32 @< The invalid second
        ) severity warning high id 10 format "Provided second is invalid should be in [0, 59]: {}"

        @ AlarmSet event indicates that the alarm was set successfully
        event AlarmSet(
            ID: U16 @< ID of the set alarm
            t: Drv.TimeData @< Time for the set alarm
        ) severity activity high id 11 format "Alarm set on RTC with ID {}, time: {}"

        @ AlarmNotSet event indicates that the alarm was not set successfully
        event AlarmNotSet(
            t: Drv.TimeData @< Time for the alarm that was not set
            rc: I32 @< Return code from the RTC driver
        ) severity warning high id 12 format "Alarm not set on RTC for time: {}, return code: {}"

        @ AlarmTriggered event indicates that an alarm was triggered
        event AlarmTriggered(
            ID: U16 @< ID of the triggered alarm
        ) severity activity high id 13 format "Alarm with ID {} triggered"

        @ AlarmCanceled event indicates that an alarm was canceled successfully
        event AlarmCanceled(
            ID: U16 @< ID of the canceled alarm
        ) severity activity high id 14 format "Alarm with ID {} canceled"

        @ AlarmNotCanceled event indicates that an alarm was not canceled successfully
        event AlarmNotCanceled(
            ID: U16 @< ID of the alarm that was not canceled
            rc: I32 @< Return code from the RTC driver
        ) severity warning high id 15 format "Alarm with ID {} not canceled, return code: {}"

        @ AlarmHardwareError event indicates hardware issues with the RTC
        event AlarmHardwareError(
            ID: U16 @< ID of the alarm that had a hardware error
            rc: I32 @< Return code from the RTC driver
        ) severity warning high id 16 format "Alarm with ID {} had a hardware error, return code: {}"

        ### PORTS ###

        @ Port for canceling running sequences when RTC time is set
        @ Connected to seqCancelIn ports of Command, Payload, and SafeMode sequencers
        output port cancelSequences: [3] Svc.CmdSeqCancel

        @ Port to indicate an alarm was triggered
        output port alarmTriggered: [1] Drv.AlarmTriggered

        ###############################################################################
        # Standard AC Ports: Required for Channels, Events, Commands, and Parameters  #
        ###############################################################################

        @ Port for getting parameters
        param get port prmGetOut

        @ Port for setting parameters
        param set port prmSetOut

        @ Port for requesting the current time
        time get port timeCaller

        @ Port for sending command registrations
        command reg port cmdRegOut

        @ Port for receiving commands
        command recv port cmdIn

        @ Port for sending command responses
        command resp port cmdResponseOut

        @ Port for sending textual representation of events
        text event port logTextOut

        @ Port for sending events to downlink
        event port logOut
    }
}
