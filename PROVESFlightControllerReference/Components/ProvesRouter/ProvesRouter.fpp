module Svc {
    @ Routes packets deframed by the Deframer to the rest of the system
    passive component ProvesRouter {

        enum AllocationReason : U8{
            FILE_UPLINK,  @< Buffer allocation for file uplink
            USER_BUFFER   @< Buffer allocation for user handled buffer
        }

        # ----------------------------------------------------------------------
        # Router Interface
        # ----------------------------------------------------------------------

        @ Receiving data (Fw::Buffer) to be routed with optional context to help with routing
        sync input port dataIn: Svc.ComDataWithContext

        @ Port for returning ownership of data (includes Fw.Buffer) received on dataIn
        output port dataReturnOut: Svc.ComDataWithContext

        @ Port for sending file packets as Fw::Buffer (ownership passed to receiver)
        output port fileOut: Fw.BufferSend

        @ Port for receiving ownership back of buffers sent on fileOut
        sync input port fileBufferReturnIn: Fw.BufferSend

        @ Port for sending command packets as Fw::ComBuffers
        output port commandOut: Fw.Com

        @ Port for receiving command responses from a command dispatcher (can be a no-op)
        sync input port cmdResponseIn: Fw.CmdResponse

        @ Port for forwarding non-recognized packet types
        @ Ownership of the buffer is retained by the ProvesRouter, meaning receiving
        @ components should either process data synchronously, or copy the data if needed
        output port unknownDataOut: Svc.ComDataWithContext

        @ Port for allocating buffers
        output port bufferAllocate: Fw.BufferGet

        @ Port for deallocating buffers
        output port bufferDeallocate: Fw.BufferSend

        @ Port to signal that a packet has been authenticated and routed
        output port packetRouted: Fw.Signal

        ### Events ###

        @ An error occurred while serializing a com buffer
        event SerializationError(
                status: U32 @< The status of the operation
            ) \
            severity warning high \
            format "Serializing com buffer failed with status {}"

        @ An allocation error occurred
        event AllocationError(reason: AllocationReason) severity warning high \
            format "Buffer allocation for {} failed"

        ### Telemetry ###

        @ Telemetry count of routed packets
        telemetry RoutedPackets : U32

        @ Telemetry count of bypassed packets
        telemetry BypassedPackets : U32

        @ Telemetry count of rejected packets
        telemetry RejectedPackets : U32

        ###############################################################################
        # Standard AC Ports for Events
        ###############################################################################

        @ Port for requesting the current time
        time get port timeCaller

        @ Port for sending textual representation of events
        text event port logTextOut

        @ Port for sending events to downlink
        event port logOut

        @ Port for sending telemetry channels to downlink
        telemetry port tlmOut

        @ Parameter get port
        param get port prmGetOut

        @ Parameter set port
        param set port prmSetOut

    }
}
